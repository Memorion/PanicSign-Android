apply plugin: 'com.android.application'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 3

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "de.bastianrinsche.panicsign"
        minSdkVersion 16
        targetSdkVersion 28

        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        archivesBaseName = "PanicSign-$versionName"

        vectorDrawables.useSupportLibrary = true

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug'
            storeFile file('../debug.keystore')
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            Properties secrets = new Properties()
            secrets.load(project.rootProject.file('release.properties').newDataInputStream())

            keyAlias "release"
            storeFile file("../release.keystore")
            keyPassword secrets["key_password"]
            storePassword secrets["keystore_password"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.firebase:firebase-appindexing:18.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup:seismic:1.0.2'
}
