apply plugin: 'com.android.application'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 3

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "de.bastianrinsche.panicsign"
        minSdkVersion 14
        targetSdkVersion 26

        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        archivesBaseName = "PanicSign-$versionName"

        vectorDrawables.useSupportLibrary = true

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug'
            storeFile file('../debug.keystore')
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            Properties secrets = new Properties()
            secrets.load(project.rootProject.file('release.properties').newDataInputStream())

            keyAlias "release"
            storeFile file("../release.keystore")
            keyPassword secrets["key_password"]
            storePassword secrets["keystore_password"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.google.firebase:firebase-appindexing:11.4.2'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:percent:26.1.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup:seismic:1.0.2'
}
